
// Two Tanks Overconstrained Helper Variable Example
//
// Authors:  Michal Konecny and Jan Duracz
// Note:     Run using Semantics -> Enclosure

model Main(simulator)  =
initially 
  x1 = 1, x1' = 2,  
  x2 = 1, x2' = -3,
  x12 = 2, x12' = -1, // Helper variable added!
  mode = "Fill1" 
always
  match mode with [
    "Fill1" claim x1  >= 0&& 
            x2  >= 0&& 
            x12 >= 0&& 
            x12 == x1 + x2 -> 
       // Constraint added
      if x2 == 0 then mode+ = "Fill2" noelse, 
      x1'  = 2, 
      x2'  = -3,
      x12' = -1
  | "Fill2" claim x1 >= 0&& 
            x2 >= 0&& 
            x12 >= 0&& 
            x12 == x1 + x2 -> 
        // Constraint added
      if x1 == 0 then mode+ = "Fill1" noelse, 
      x1'  = -2, 
      x2'  = 1,
      x12' = -1 ], // x12' == x1' + x2' 
  simulator.endTime+ = 2.5,
  simulator.minSolverStep+ = 2.5,
  simulator.minLocalizationStep+ = 0.00001,
  simulator.minComputationImprovement+ = 0

// Note:  An additional constraint, based on a helper
//        variable, stabilizes the post-Zeno 
//        enclosure.
//
// Note:  The dynamics and constraints added based on 
//        the helper variable x12 express that the 
//        sum of x1 and x2 is decreasing.
