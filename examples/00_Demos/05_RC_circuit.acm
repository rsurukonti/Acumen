// Battery charging and discharging
//
// Author:  Anita Sant'Anna and Fei Xu, 2016
//
// Run this example, view Plot, and then 3D

// This example models a simple RC circuit 
// described by the differential equation: 
//
//    Vc'=(Vin-Vc)/(R*C),
//      where:
//       Vc: voltage over capacitor
//       Vin: input voltage
//       R: resistance
//       C: capacitance

model CircuitRC(C,R,POS,plot)=
initially 
  Vc = 0, Vc' = 0, Vin = 5, _plot = (),
  _3D = (Cylinder center=POS radius= 0.1 length=0 color = (0,1,0)  rotation=(0,0,0)) 
always 
  Vc'=(Vin-Vc)/(R*C),
  if (plot) then
    _plot = (Vc, Vin)
  noelse,
  _3D=(Cylinder center=POS radius= 0.1 length=Vc/2 color = (0,1,0)  rotation=(3.1416/2,0,0)) 



model Main(simulator) = 
initially 
  // Create two RC circuits with different parameters
  circuit1  =  create  CircuitRC(0.02,10,(0,0,0),true), 
  circuit2  =  create  CircuitRC(0.2,10,(1,0,0),false), 
  t = 0, t' = 1, mode = "charge",
  _3DView = ((0,-10,2),(0,0,0))
always
  t'=1,
  // The input voltage to the circuits 
  //   will alternate between 5V and 0V every second
  match mode with [
    "charge" ->
      circuit1.Vin=5,
      circuit2.Vin=5,
      if t >= 1 then
        mode + = "discharge" 
      noelse
  | "discharge" ->
      circuit1.Vin=0,
      circuit2.Vin=0,
      if t>=2 then
        t+ =0, 
        mode + = "charge" 
      noelse ]
 