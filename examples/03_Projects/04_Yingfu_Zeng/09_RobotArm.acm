// Example:  Robot Animation 
// Illustrates:
//    - how acumen 3D visulization works

model Head(p) = 
initially
  p' = (0,0,0) ,
  mode = "Moving" ,
  _3D = (Sphere center=(0,0,1.2) size=0.3 color= (1,1,1) rotation=(0,0,0),
         Sphere center=(0,0,1.2) size=0.05 color=(0,0,1) rotation=(0,0,0),
         Sphere center=(0,0,1.2) size=0.05 color=(0,0,1) rotation=(0,0,0),
         Sphere center=(0,0,1.2) size=0.04 color=(0,0,0) rotation=(0,0,0))
always
  _3D = (Sphere center=p size=0.3 color=(1,1,1) rotation=(0,0,0),
         // Different offsets
         Sphere center=p+(0.12,-0.32,0.15) size=0.05 color=(0,0,1) rotation=(0,0,0),
         Sphere center=p+(-0.12,-0.32,0.15) size=0.05 color=(0,0,1) rotation=(0,0,0),
         Sphere center=p+(0,-0.25,-0.1) size=0.04 color=(0,0,0) rotation=(0,0,0))

model UpperPart(p) = 
initially
  p' = (0,0,0) ,
  mode = "Moving" ,  
  _3D = (Cylinder center=(0,0,0.7) radius= 0.2 length=1.2  color= (1,1,1) rotation=(0,0,0),
         Sphere   center=(0,0,1.2) size=0.25 color=(0,0,0.5) rotation=(0,0,0),
         Sphere   center=(0,0,1.2) size=0.25 color=(0,0,0.5) rotation=(0,0,0),
         Cylinder center=(0,0,1.2) radius= 0.1 length=0.8  color=(1,1,1) rotation=(0,0,0),
         Cylinder center=(0,0,1.2) radius= 0.1 length=0.8  color=(1,1,1) rotation=(0,0,0)),
  armAngle  =  1 ,       armAngle'  =  0 , armAngle''  =  0 ,
  leftP     =  (0,0,0) , rightP     =  (0,0,0) 
always
  _3D = (Cylinder center=p radius=0.2 length=1.2 color=(1,1,1) rotation=  (0,0,3.14/2),
         Sphere   center=p-(0.6,0,0)  size=0.25  color=(0,0,0.5) rotation=(0,0,0),
         Sphere   center=p+(0.6,0,0)  size=0.25  color=(0,0,0.5) rotation=(0,0,0),
         Cylinder center=leftP  radius=0.1 length=0.8 color=(1,1,1) rotation=(3.14/2 - armAngle,0,0),
         Cylinder center=rightP radius=0.1 length=0.8 color=(1,1,1) rotation=(3.14/2 + armAngle,0,0)) ,
  armAngle'' = -10*sin(armAngle) , 
  leftP = p + (0.6,-0.05,-0.4)  + (0,-0.45*sin(armAngle),(0.45-0.45*cos(armAngle))) ,
  rightP= p + (-0.6,-0.05,-0.4) + (0,0.45*sin(armAngle),(0.45-0.45*cos(armAngle)))

model LowerPart(p) = 
initially
  t = 0 ,t' = 0 ,
  //p = (0,0,0.4) ,
  p' = (0,0,0) ,
  mode = "Moving" ,  
  axisP           =  (0,0,0) ,
  leftLegP        =  (0,0,0) ,
  leftLeg2P       =  (0,0,0) ,
  leftBearingP    =  (0,0,0) ,
  leftBearing2P   =  (0,0,0) ,
  leftFootP       =  (0,0,0) ,
  rightLegP       =  (0,0,0) ,
  rightLeg2P      =  (0,0,0) ,
  rightBearingP   =  (0,0,0) ,
  rightBearing2P  =  (0,0,0) ,
  rightFootP      =  (0,0,0) ,
  
  _3D = (Box      center=(0,0,0.4) size=(0.6,0.5,0.5) color= (1,1,1) rotation=(0,0,0),
          //Axis
         Cylinder center=(0,0,1.2) radius= 0.16 length=0.5  color=    (1,1,1) rotation=  (0,0,0),
         Cylinder center=(0,0,1.2) radius= 0.18 length=0.5  color=    (1,1,1) rotation=  (0,0,0),  // LeftLeg
         Cylinder center=(0,0,1.2) radius= 0.1 length=0.5  color=     (1,1,1) rotation=  (0,0,0),  // LeftLeg2
         Sphere   center=(0,0,1.2) size=  0.16 color=        (0,0,0.8) rotation=(0,0,0),  // LeftBearing
         Sphere   center=(0,0,1.2) size=  0.12 color=        (0,0,0.5) rotation=(0,0,0),  // LeftBearing2
         Box      center=(0,0,1.2) size=(0.3,0.7,0.15) color=(1,1,1) rotation=  (0,0,0),  // LeftFoot
     
         Cylinder center=(0,0,1.2) radius= 0.18 length=0.5  color=    (1,1,1) rotation=  (0,0,0),  // RightLeg
         Cylinder center=(0,0,1.2) radius= 0.1 length=0.5  color=     (1,1,1) rotation=  (0,0,0),  // RightLeg2
         Sphere   center=(0,0,1.2) size=  0.16 color=        (0,0,0.8) rotation=(0,0,0),  // RightBearing
         Sphere   center=(0,0,1.2) size=  0.12 color=        (0,0,0.5) rotation=(0,0,0),  // RightBearing2
         Box      center=(0,0,1.2) size=(0.3,0.7,0.15) color=(1,1,1) rotation=  (0,0,0)) ,  // RightFoot
    
  angle  =  0 ,angle'  =  0 
always
  t'=1 ,

  leftBearingP  = axisP + (-0.2,0,-0.12) ,
  leftBearing2P = leftLegP + (0,-0.25*sin(angle),-0.25*cos(angle)) ,
  leftLegP      = leftBearingP + (0,0,-0.28)+(0,-0.35*sin(angle),(0.35-0.35*cos(angle))) ,
  leftLeg2P     = leftBearing2P + (0,-0.05,-0.28) ,
  leftFootP     = leftLeg2P     + (0,-0.1,-0.1-0.25) , 
  rightBearingP = axisP + (0.2,0,-0.12) ,
  rightBearing2P= rightLegP+(0,-0.25*sin(-angle),-0.25*cos(angle)) ,
  rightLegP     = rightBearingP+(0,0,-0.28)+(0,-0.35*sin(-angle),(0.35-0.35*cos(angle))) ,
  rightLeg2P    = rightBearing2P+(0,0,-0.28) ,
  rightFootP    = rightLeg2P+(0,-0.1,-0.1-0.25) , 

  _3D = (Box      center=p size=(0.6,0.5,0.5) color=(1,1,1) rotation=(0,0,0),
         //Axis
         Cylinder center=axisP      radius=0.16 length=0.5 color=(1,1,1) rotation=(0,0,3.14/2),
         Cylinder center=leftLegP   radius=0.18 length=0.5 color=(1,1,1) rotation=(pi/2-angle,0,0),  // LeftLeg
         Cylinder center=leftLeg2P  radius=0.1  length=0.5 color=(1,1,1) rotation=(pi/2-0.5*angle,0,0),  // LeftLeg2
         Sphere   center=leftBearingP  size=0.16 color=(0,0,0.8) rotation=(0,0,0),  // LeftBearing
         Sphere   center=leftBearing2P size=0.12 color=(0,0,0.5) rotation=(0,0,0),  // LeftBearing2
         Box      center=leftFootP size=(0.3,0.7,0.15) color=(1,1,1) rotation=(-0.3*angle,0,0),  // LeftFoot
     
         Cylinder center=rightLegP  radius=0.18 length=0.5 color=(1,1,1) rotation=(pi/2+angle,0,0),  // RightLeg
         Cylinder center=rightLeg2P radius=0.1  length=0.5 color=(1,1,1) rotation=(pi/2+0.5*angle,0,0),  // RightLeg2
         Sphere   center=rightBearingP  size=0.16 color=(0,0,0.8) rotation=(0,0,0),  // RightBearing
         Sphere   center=rightBearing2P size=0.12 color=(0,0,0.5) rotation=(0,0,0),  // RightBearing2
         Box      center=rightFootP size=(0.3,0.7,0.15) color=(1,1,1) rotation=(0.3*angle,0,0)) ,  // RightFoot

  axisP=p+(0,0,-0.35) ,
  if t<3 then
    angle'=0.2 
  noelse ,
  if t>=3 && t<9 then
    angle'=-0.2 
  noelse ,
  if t>9 && t<12 then
    angle'=0.2
  noelse ,
  if t>12 then 
    t+ =0 
  noelse 

model Robot(p1,p2,p3) = 
initially
  t = 0 ,t' = 1 ,
  head       =  create Head(p1) ,
  upperPart  =  create UpperPart(p2) ,
  lowerPart  =  create LowerPart(p3) 
always
  t' = 1 ,
  head.p'      = (0,-0.2,0) ,
  upperPart.p' = (0,-0.2,0) ,
  lowerPart.p' = (0,-0.2,0)

model Main(simulator) = 
initially
  mode  =  "Init" 
  //_3D  =  (Box center=(0,0,-1.3) size=(8,20,0.1) color=(0.9,0.2,0.2) rotation=(0,0,0)) ,
always
  match mode with [
    "Init" ->      
      simulator.timeStep + = 0.01 ,
      simulator.endTime + = 10 ,
      create Robot((0,2,1.2), (0,2,0.7), (0,2,0.4)) ,
      create Robot((-2,2,1.2),(-2,2,0.7),(-2,2,0.4)) ,  
      create Robot((2,2,1.2), (2,2,0.7), (2,2,0.4)) , 
      create Robot((0,0,1.2), (0,0,0.7), (0,0,0.4)) ,
      create Robot((-2,0,1.2),(-2,0,0.7),(-2,0,0.4)) ,  
      create Robot((2,0,1.2), (2,0,0.7), (2,0,0.4)) ,  
    
      create Robot((0,-2,1.2), (0,-2,0.7), (0,-2,0.4)) ,
      create Robot((-2,-2,1.2),(-2,-2,0.7),(-2,-2,0.4)) ,  
      create Robot((2,-2,1.2), (2,-2,0.7), (2,-2,0.4)) ,   
      mode + = "Persist"
  | "Persist" -> ]
 
