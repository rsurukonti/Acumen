model RLcircuit(R,L) = 
initially 
  i = 0 , i' = 0 , v = 0 , e = 0 
always
  i' =(v - e - R*i)/L 

model ElectricMotor(Jeq,kE,kT,TL) = 
initially 
  i = 0 , wm = 0 , wm' = 0 , e = 0  
always
  wm'=(kT*i - TL)/Jeq ,
  e=wm*kE

model PIcontroller(kP,kI,kPWM) = 
initially 
  h = 0 , int_h = 0 , int_h' = 0 , i = 0 , int_i = 0 , int_i' = 0 , v = 0 
always
  int_i'=i ,
  int_h'=h ,
  v=kPWM*(kP*(h-i)+kI*(int_h-int_i))

model Main(simulator) = 
initially
  motor  =  create ElectricMotor(0.0002,0.1,0.1,0) ,
  circuit  =  create RLcircuit(4,2) ,
  controller  =  create PIcontroller(50,100,6) ,
  mode  =  1 , t = 0 , t' = 1 
always
  t'=1 ,
  controller.i=circuit.i ,
  circuit.v=controller.v ,
  circuit.e=motor.e ,
  motor.i=circuit.i ,

  simulator.timeStep+ =0.001 ,
  simulator.endTime+ =1 ,

  match mode with [
    1 ->
      controller.h=0 ,
      if t>=0.1 then mode+ =2 noelse
  | 2 ->
      controller.h=10 ,
      if t>=0.2 then mode+ =3 noelse
  | 3 ->
      controller.h=0 ]
