// Bouncing ball with hole
// Date: 2015-03-13
// Authors: Shota Matsumoto, Yoshiaki Wakatsuki, 
//          Takeshi Ito, Yingfu Zeng, Adam Duracz 
// Note: Translation from HydLa model, illustrating
//       simulation with uncertain initial values.

model Main(_) =
  initially
    x = 0, x' = [1 .. 5], x'' = 0,
    y = 10, y' = 0, y'' = -10
  always
    // FALL
    y'' = -10, 
    // BOUNCE
    if y == -7 || (x <= 7 || x >= 10) && y == 0
    then y'+ = -4/5 * y'
    noelse,
    // XCONST
    x'' = 0,
    // XBOUNCE
    if (x == 7 || x == 10) && y < 0
    then x'+ = -x'
    noelse 

// Original HydLa model
//INIT <=> y = 10 & y' = 0 & x = 0 & 1 <= x' <= 5.
//FALL <=> [](y'' = -10).
//XCONST <=> [](x'' = 0).
//XBOUNCE <=> []((x- = 7 | x- = 10) & y- < 0 => x' = -x'-). 
//BOUNCE <=> [](y- = -7 | (x- <= 7 | x- >= 10) & y- = 0 => y' = -(4/5) * y'-).
//
//INIT, FALL << BOUNCE, XCONST << XBOUNCE.